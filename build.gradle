plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}
apply plugin: 'application'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDir 'res'
            }
        }
    }

    tasks.withType(Jar) {
        destinationDir = file("$rootDir/jars")
    }
}

jar {
    enabled = false
}

dependencies {
    compile project(':api')
    compile project(':core')
    compile project(':dev_plugin')
    compile project(':stock_plugins')
}

jar.dependsOn shadowJar

shadowJar {
    dependencies {
        exclude(dependency(':dev_plugin'))
    }
    mergeServiceFiles()
    destinationDir = file("$rootDir/jars")
    baseName = 'gnupolpot'
    classifier = ''
    manifest.attributes 'Main-Class': 'net.avensome.dev.gnupolpot.core.PolpotApp'
}

clean {
    delete 'jars'
}

run {
    mainClassName = 'net.avensome.dev.gnupolpot.core.PolpotApp'
}
